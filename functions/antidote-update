#!/usr/bin/env zsh
setopt local_options no_notify no_monitor
0=${(%):-%x}

function __antidote_update {
  bundledir="$1"
  local url=$(_antidote_git -C "$bundledir" config remote.origin.url)
  local oldsha=$(_antidote_git -C "$bundledir" rev-parse --short HEAD)
  echo "antidote: updating: $url"
  _antidote_git -C "$bundledir" pull --quiet --ff --rebase --autostash
  local newsha=$(_antidote_git -C "$bundledir" rev-parse --short HEAD)
  if [[ $oldsha != $newsha ]]; then
    echo "antidote: updated: $url $oldsha -> $newsha"
  fi
}

# update antidote
echo "Updating antidote..."
_antidote_git -C "${0:A:h:h}" pull --ff --rebase --autostash

echo "Updating bundles..."
local ANTIDOTE_HOME=$(antidote-home)
local d
for d in $ANTIDOTE_HOME/**/.git/..; do
  __antidote_update "${d:A}" &
done
wait
